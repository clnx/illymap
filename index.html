<!DOCTYPE html>
<html>
  <head>
    <meta charset='utf-8' />
    <meta name="description" content="Illymap : Illyriad Map Smelter" />
    <link rel="stylesheet" type="text/css" media="screen" href="stylesheets/stylesheet.css">
    <script type="text/javascript" src="javascripts/jquery-1.7.2.min.js"></script>
    <script type="text/javascript" src="javascripts/gl-matrix-1.3.5-min.js"></script>
    <script type="text/javascript" src="javascripts/main.js"></script>
    
    <script id="main-vs" type="x-shader/x-vertex">
      attribute vec3 a_vtx_pos;
      attribute vec2 a_tex_pos;

      uniform mat4 mv_mat;
      uniform mat4 p_mat;

      varying vec2 tex_pos;

      void main(void) {
        gl_Position = p_mat * mv_mat * vec4(a_vtx_pos, 1.0);
        tex_pos = a_tex_pos;
      }
    </script>
    
    <script id="main-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler_bg;
      uniform sampler2D sampler_towns;

      uniform bool show_bg;
      uniform bool grey_bg;
      uniform bool show_towns;

      varying vec2 tex_pos;

      void main(void) {
        vec4 col = vec4(0., 0., 0., 1.);
        if (show_towns && texture2D(sampler_towns, tex_pos) != 0.) {
          gl_FragColor = vec4(0., 1., 0., 1.);
          return;
        }
        if (show_bg) {
          col = texture2D(sampler_bg, tex_pos);
          if (grey_bg) {
            float grey = dot(col.rgb, vec3(.34, .5, .16));
            col = vec4(grey, grey, grey, 1.);
          }
        }
        gl_FragColor = col;
      }
    </script>
    
    <script id="overlay-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler;

      varying vec2 tex_pos;

      void main(void) {
        gl_FragColor = texture2D(sampler, tex_pos);
      }
    </script>
    
    <script id="stars-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler;

      uniform vec3 color;

      varying vec2 tex_pos;

      void main(void) {
        gl_FragColor = texture2D(sampler, tex_pos) * vec4(color, 1.);
        gl_FragColor.a = smoothstep(0., 1., dot(gl_FragColor.rgb, vec3(.34, .5, .16)));
      }
    </script>

    <script id="gauss_h-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler_towns;
      uniform sampler2D sampler_pids;
      uniform sampler2D sampler_prev;

      uniform float kernel[{KERNEL_UNIFORM_SIZE}];
      uniform int start_index;
      uniform int end_index;
      uniform float filter;
      uniform bool use_sampler_prev;

      varying vec2 tex_pos;

      void main(void) {
        float sum = 0.;
        for (int k=0; k<{KERNEL_UNIFORM_SIZE}; k++) {
          vec2 pos = vec2(tex_pos.s + 0.001 * float(k + start_index), tex_pos.t);
          if (pos.s < 0. || pos.s > 1.) continue;
          vec4 pid = texture2D(sampler_pids, pos);
          vec4 sample = texture2D(sampler_towns, pos);
          if (filter == 0. || filter == pid.r)
            sum += kernel[k] * sample.r;
          if (filter == 0. || filter == pid.g)
            sum += kernel[k] * sample.g;
          if (filter == 0. || filter == pid.b)
            sum += kernel[k] * sample.b;
          if (filter == 0. || filter == pid.a)
            sum += kernel[k] * sample.a;
          if (k + start_index == end_index) break;
        }
        if (use_sampler_prev)
          sum += texture2D(sampler_prev, tex_pos).r;
        gl_FragColor = vec4(sum, 0., 0., 1.);
      }
    </script>
    
    <script id="gauss_v-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler_gauss_h;
      uniform sampler2D sampler_comp;
      uniform sampler2D sampler_prev;

      uniform float kernel[{KERNEL_UNIFORM_SIZE}];
      uniform int start_index;
      uniform int end_index;
      uniform float filter;
      uniform bool compare;
      uniform bool use_sampler_prev;

      varying vec2 tex_pos;
      
      const float min_pop = .5;

      void main(void) {
        float sum = 0.;
        for (int k=0; k<{KERNEL_UNIFORM_SIZE}; k++) {
          vec2 pos = vec2(tex_pos.s, tex_pos.t + .001 * float(k + start_index));
          if (pos.t < 0. || pos.t > 1.) continue;
          sum += kernel[k] * texture2D(sampler_gauss_h, pos).r;
          if (k + start_index == end_index) break;
        }
        if (use_sampler_prev)
          sum += texture2D(sampler_prev, tex_pos).r;
        gl_FragColor = vec4(sum, filter, 0., 1.);
        if (compare) {
          vec4 sample = texture2D(sampler_comp, tex_pos);
          if (sample.r > sum && sample.r > min_pop) gl_FragColor = sample;
          else if (sum < min_pop) gl_FragColor.g = 0.;
        } 
      }
    </script>
    
    <script id="max_h-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler;

      varying vec2 tex_pos;
      
      // 16bit.16bit fixed point encoding
      vec4 float2fixed(float val) {
        float ip = floor(val);
        float ip0 = floor(ip / 256.);
        float ip1 = ip - ip0 * 256.;
        float fp = floor(fract(val) * 65536.);
        float fp0 = floor(fp / 256.);
        float fp1 = fp - fp0 * 256.;
        return vec4(ip0, ip1, fp0, fp1) / 255.;
      }

      void main(void) {
        float max_val = 0.;
        for (int i=0; i<10; i++) {
          float val = texture2D(sampler, vec2(tex_pos.s + float(i) * .001, tex_pos.t)).r;
          if (val > max_val) max_val = val;
        }
        gl_FragColor = float2fixed(max_val);
      }
    </script>
    
    <script id="max_v-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler;

      varying vec2 tex_pos;
      
      // 16bit.16bit fixed point encoding
      vec4 float2fixed(float val) {
        float ip = floor(val);
        float ip0 = floor(ip / 256.);
        float ip1 = ip - ip0 * 256.;
        float fp = floor(fract(val) * 65536.);
        float fp0 = floor(fp / 256.);
        float fp1 = fp - fp0 * 256.;
        return vec4(ip0, ip1, fp0, fp1) / 255.;
      }
      
      float fixed2float(vec4 val) {
        float f = val.r * 255. * 256. + val.g * 255.;
        f += (val.b * 255. * 256. + val.a * 255.) / 65536.;
        return f;
      }
      
      void main(void) {
        float max_val = 0.;
        for (int i=0; i<10; i++) {
          float val = fixed2float(texture2D(sampler, vec2(tex_pos.s, tex_pos.t + float(i) * .001)));
          if (val > max_val) max_val = val;
        }
        gl_FragColor = float2fixed(max_val);
      }
    </script>
    
    <script id="heat-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler;
      uniform float gamma;
      uniform float max_value;

      varying vec2 tex_pos;
      
      vec4 val2rgb(float val, float max_val) {
        vec4 color = vec4(0., 0., 0., 0.5);
        float factor = 0., r = 0., g = 0., b = 0.;
        float wavelen = 380. + 400. * abs(val) / max_val;
        if (wavelen < 440.) { r = -(wavelen - 440.) / 60.; b = 1.; }
        else if (wavelen < 490.) { g = (wavelen - 440.) / 50.; b = 1.; }
        else if (wavelen < 510.) { g = 1.; b = -(wavelen - 510.) / 20.; }
        else if (wavelen < 580.) { r = (wavelen - 510.) / 70.; g = 1.; }
        else if (wavelen < 645.) { r = 1.; g = -(wavelen - 645.) / 65.; }
        else if (wavelen < 780.) r = 1.;
        if (wavelen < 420.) factor = 0.3 + 0.7 * (wavelen - 380.) / 40.;
        else if (wavelen < 700.) factor = 1.;
        else if (wavelen < 780.) factor = 0.3 + 0.7 * (780. - wavelen) / 80.;
        if (r > 0.) color.r = pow(r * factor, gamma);
        if (g > 0.) color.g = pow(g * factor, gamma);
        if (b > 0.) color.b = pow(b * factor, gamma);
        return color;
      }
      
      void main(void) {
        gl_FragColor = val2rgb(texture2D(sampler, tex_pos).r, max_value);
      }
    </script>
    
    <script id="partition-fs" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D sampler;
      uniform float sel_par;
      uniform bool races_mode;
      uniform vec3 par_colors[15];
      
      varying vec2 tex_pos;
      
      bool proc_tex(vec2 coord, int tex_idx) {
        if (tex_idx == 0) return false;
        --tex_idx;
        int size = tex_idx / 5;
        int mode = tex_idx - 5 * size;
        size = 4 * (size + 2);
        int x = int(coord.x * 1000.);
        int y = int(coord.y * 1000.);
        int d1 = y - x, d2 = x + y;
        int rx = x - size * (x / size);
        int ry = y - size * (y / size);
        if (mode == 3) return rx == 0;
        else if (mode == 4) return ry == 0;
        else if (mode == 0) return d1 - size * (d1 / size) == 0;
        else if (mode == 1) return d2 - size * (d2 / size) == 0;
        else if (mode == 2) return rx == 0 && ry == 0 || rx == 1 && ry == 0 ||
          rx == 2 && ry == 0 || rx == 1 && ry == 1 || rx == 1 && ry == size - 1;
        return false;
      }

      void main(void) {
        gl_FragColor = vec4(0., 0., 0., 1.);
        float pid = texture2D(sampler, tex_pos).g;
        if (pid == 0.) return;
        int id = int(pid * 255.) - 1;
        bool selected = (sel_par > 0. && sel_par == pid);
        int tex_idx = id / 15;
        int col_idx = id - 15 * tex_idx;
        vec3 color = vec3(0.);
        for (int i=0; i<15; i++) {
          color = par_colors[i];
          if (i == col_idx) break;
        }
        if (selected) {
          if (!races_mode) color = vec3(0.);
          if (proc_tex(tex_pos, tex_idx > 0 ? tex_idx : 3))
            color = vec3(1.);
        }
        else {
          if (proc_tex(tex_pos, tex_idx))
            color = vec3(0.);
        }
        gl_FragColor = vec4(color, .5);
      }
    </script>

    <title>Illymap - Illyriad Heat & Partition Maps</title>
  </head>

  <body>
    <div class="outer">
      <header class="inner">
        <div id="header_left">
          <h1>Illymap</h1>
          <h2><a href="http://www.illyriad.co.uk/" target="_blank">Illyriad</a> Heat &amp; Partition Maps</h2>
        </div>
        <div id="header_right">
          <form name="ui_form">
            <div class="ui_panel">
              <div id="server_info" class="tooltip" data-tooltip="Double-click on the map to open a new tab with the Illyriad map page centered on the position.">server:?<br/>date:?<br/></div>
            </div>
            <div class="ui_panel">
              <label id="overlay_mode_label" for="overlay_mode" class="tooltip" data-tooltip="Select the map overlay mode. Heat maps should be completed instantly. Alliance Partition is the heaviest GPU computation, with time ~ sigma. At sigma = 15 it can take ~4 sec on a HD7950 during which your screen may freeze for short intervals. Don't worry, just let it finish ;)">Overlay:</label>
              <select id="overlay_mode">
                <option value="none" selected="selected">None</option>
                <option value="pop">Population</option>
                <option value="pop:E">Elves</option>
                <option value="pop:H">Humans</option>
                <option value="pop:D">Dwarves</option>
                <option value="pop:O">Orcs</option>
                <option value="par:races">Races Partition</option>
                <option value="par:alliances">Alliances Partition</option>
              </select><br />
              <label id="std_dev_label" for="std_dev" class="tooltip" data-tooltip="The population of each city is spread out according to a normal (Gaussian) distribution. With this option you can change its standard deviation. Higher values will give a smoother appearence to the overlays. Units are map-units, but the map is 1:2 vs. in-game squares.">Sigma:</label>
              <select id="std_dev">
                <option value="2">2</option>
                <option value="5">5</option>
                <option value="10">10</option>
                <option value="15" selected="selected">15</option>
                <option value="20">20</option>
                <option value="30">30</option>
                <option value="50">50</option>
              </select><br />
              <label id="gamma_label" for="gamma" class="tooltip" data-tooltip="Gamma factor for the false color overlays (heat maps). Smaller values will create sharper color boundaries.">Gamma:</label>
              <select id="gamma">
                <option value="0.1">0.1</option>
                <option value="0.2">0.2</option>
                <option value="0.3">0.3</option>
                <option value="0.4">0.4</option>
                <option value="0.5">0.5</option>
                <option value="0.6">0.6</option>
                <option value="0.7">0.7</option>
                <option value="0.8" selected="selected">0.8</option>
                <option value="0.9">0.9</option>
                <option value="1.0">1.0</option>
              </select>
            </div>
            <div class="ui_panel">
              <input id="show_map" type="checkbox" checked="checked" /> <label for="show_map" class="tooltip" data-tooltip="Show the background map.">Show Map</label><br />
              <input id="show_towns" type="checkbox" /> <label for="show_towns" class="tooltip" data-tooltip="Show towns as green dots on the map.">Show Towns</label><br />
              <input id="show_capitals" type="checkbox" checked="checked" /> <label for="show_capitals" class="tooltip" data-tooltip="Show alliance capitals as yellow stars. Mouse over them to see more information.">Show Capitals</label>
            </div>
            <div id="xml2json" class="ui_panel"><a id="xml2json_btn" href="#" class="tooltip" data-tooltip="For hosters. To update data/data.json: After uploading newer versions of datafile_alliances.xml and datafile_towns.xml into the data folder, click this to generate the json data. After generation, paste the data from the screen into the file.">XML2JSON</a></div>
          </form>
        </div>
      </header>
    </div>

    <div class="outer">
      <section id="main_content" class="inner">
        <canvas id="map" width="1000" height="1000"></canvas>
        <div id="pos_info">[ 0 : 0 ]</div>
        <div id="jsondiv"></div>
        <div id="infobox"></div>
      </section>
    </div>

    <div class="outer">
      <footer class="inner">
        <p>&copy; 2012 by The Interwebs</p>
        <p><a href="https://github.com/clnx/illymap" target="_blank">Project</a> hosted on GitHub</p>
        <br />
      </footer>
    </div>
  </body>
</html>
